# Problemes especialment complexos de la desambiguació
#	- son: pos o vbser o nom
#	- a: vbhaver o prep
#	- an: vbhaver o nom
#	- se: prn.pro.ref o cnjadv
#	- es: det (aran) o vbser

# Firstly, we need to define what tags should be considered sentence delimiters. For this example, only full stop is set as delimiter.
DELIMITERS = "<.>" ;

LIST BOS = (>>>) ;
LIST EOS = (<<<) ;

#Categorias
LIST V = vblex vbser vbhaver vbaux vbmod ;
LIST Ser = vbser ;
LIST Conjco = cnjcoo ;
LIST Conjsub = cnjsub ;
LIST Conjadv = cnjadv ;
LIST Enon = enon ;
LIST Rel = rel ;
LIST N = n ;
LIST NP = np ;
LIST AJ = adj ;
LIST DET = det ;
LIST NUM = num ;
LIST PREP = pr ;
LIST PRON = prn ;
LIST PERS = pers ;
LIST AV = adv ;
LIST PREAV = preadv ;
LIST Excl = "!" ;
LIST PRONPers = (prn p1) (prn p2) (prn p3); 
LIST IJ = ij ;
LIST PREDET = predet ;
LIST Pos = pos ;
LIST DETPos = (det pos);
LIST SENT = sent ;

#Jos=categorias
LIST Pro = pro ;
LIST VBger = ger ;
LIST VBimp = imp ;
LIST PRencl = enc ;
LIST VBinf = inf ;
LIST VBpp = pp ;
LIST Npl = (n pl);
LIST AJpl = (adj pl);
LIST Ns = (n s);
LIST AJs = (adj s);
LIST Nm = (n m);
LIST AJm = (adj m);
LIST Nf = (n f);
LIST AJf = (adj f);
LIST Vconj = pri prs pii pis imp fti ifi cni ;
LIST Itg = itg ;
LIST DETind = (det ind);

#Listas
SET intrprop = Conjco OR Itg ; #cnjcoo cnjsub rel itg "," ";" sent adv;
LIST motsdetpl = (det pl) (pr pl);
LIST motsdetsg = (det sg) (pr sg);
LIST motsdetf = (det f) (pr f);
LIST motsdetm = (det m) (pr m);

LIST Masc = m mf ;
LIST Fem = f mf ;

SET MascSg = (m sg) OR (m sp) OR (mf sg) OR (mf sp) ;
SET MascPl = (m pl) OR (m sp) OR (mf pl) OR (mf sp) ;
SET FemSg = (f sg) OR (f sp) OR (mf sg) OR (mf sp) ;
SET FemPl = (f pl) OR (f sp) OR (mf pl) OR (mf sp) ;

LIST P1 = p1 ;
LIST P2 = p2 ;
LIST P3 = p3 ;
LIST Pers = p1 p2 p3 ;

SET CM = (cm) OR (lpar) OR (rpar) OR ("<;>") ;
LIST Excl = "!" ;

#Mots
LIST En = ("en" prn);
LIST Un = "un" ;
LIST An = "an" ;
LIST Eth = "eth" ;
LIST Nostre = "nòstre" "nòste" ;
LIST Vostre = "vòstre" "vòste" ;
LIST Pas = "pas" ;
LIST Que = "que" ;
LIST Se = "se" ;
LIST Las = "las" ;
LIST Mas = "mas" ;

LIST Excl = "!" ;

LIST unknown = ("\\*.*"r) ;
LIST unknownMajIni = ("\\*[A-ZÀÁÈÉíÒÓÚÜÇ].*"r) ;


SECTION

#Un pronom proclitic est totjorn davant un vèrb o un autre pronom
REMOVE Pro IF (NOT 1 V OR Pro);

#En es pas un pronom s'es abans un gerondiu (amb possibilitat de pronoms entre los dos)
REMOVE En IF (1* VBger BARRIER (*) - Pro) ;

#Una conjonccion o un pronom relatiu se podon pas trobar en debuta de frasa
REMOVE Conjco IF (-1 BOS OR SENT);
REMOVE Conjsub IF (-1 BOS OR SENT);
REMOVE Rel IF (-1 BOS OR SENT);

#Un imperatiu seguis pas una conjonccion, un relatiu o un pronom personal
REMOVE VBimp IF (-1 Enon OR Conjco OR Conjsub OR Rel OR Pers);

REMOVE VBimp IF (NOT 1* Excl) ;
REMOVE VBimp IF (0 P3) ;

#Un nom pot pas seguir qu'un vèrbe, un adjectiu, un determinant, un numeral, una prepausicion o un pronom (levat los personals)
#(en titolars periodístics, però, es comença sovint per un nom)
REMOVE N IF (NOT -1 BOS OR V OR AJ OR DET OR NUM OR PREP OR PRON - PRONPers);

#Un predeterminant es pas seguit que per un determinant
REMOVE PREDET IF (NOT 1 DET);

#Los pronoms possessius son totjorn seguits per un nom, un adjectiu, un advèrb o un numeral
REMOVE DETPos IF (NOT 1 N OR AJ OR AV OR NUM OR NP OR unknown);

# L'enonciatiu es totjorn seguit d'un vèrb o d'un enclitic
REMOVE Enon IF (NOT 1 V OR PRencl);

#Una prepausicion es totjorn seguida d'un nom, pronom, adverb, numeral, infinitiu, determinant o adjectiu
REMOVE PREP IF (NOT 1 N OR PRON OR AV OR NUM OR NP OR VBinf OR DET OR AJ);

#Es pas un vèrbe se, dempuèi lo darrièr vèrbe conjugat, avèm pas agut de novèla proposicion
#REMOVE Vconj IF (NOT -1* intrprop BARRIER Vconj) ;
	# funciona pas: “Es lo primièr còp que vesi aquò”

#Un determinant es pas plural se lo nom/adjectiu que seguis es singular (o masculin feminin...)
REMOVE motsdetpl IF (1 Ns OR AJs);
REMOVE motsdetsg IF (1 Npl OR AJpl);
REMOVE motsdetf IF (1 Nm OR AJm);
REMOVE motsdetm IF (1 Nf OR AJf);


# AN (+ partipici)

SELECT V IF (0 An + MascSg) (1 VBpp + MascSg) ;
  ## aqueste mes an fach
SELECT V IF (0 An + MascSg) (1 Pas) (2 VBpp + MascSg) ;
SELECT V IF (0 An + MascSg) (1C AV) (2 VBpp + MascSg) ;

# DET

SELECT DET IF (0 DETind + MascSg) (1 N + MascSg) ;
SELECT DET IF (0 DETind + FemSg) (1 N + FemSg) ;
SELECT DET IF (0 DETind + MascPl) (1 N + MascPl) ;
SELECT DET IF (0 DETind + FemPl) (1 N + FemPl) ;
  ## un enfant
SELECT DET IF (0 DETind + MascSg) (1 AJ + MascSg) ;
SELECT DET IF (0 DETind + FemSg) (1 AJ + FemSg) ;
SELECT DET IF (0 DETind + MascPl) (1 AJ + MascPl) ;
SELECT DET IF (0 DETind + FemPl) (1 AJ + FemPl) ;
  ## un jaune
SELECT DET IF (0 DETind) (1C V) ;
SELECT DET IF (0 DETind) (1 unknown) ;
  ## pedaç, especialment quan falta molt vocabulari (endevinem que és un DET i no una forma del V unir)
REMOVE DET IF (0 Eth) (1 DET);
REMOVE DET IF (0 Eth) (1 Pas);

# NUM

SELECT NUM IF (-1 ("numéro")) (0 Un); 
REMOVE NUM IF (NOT -1 ("numéro")) (0 Un); 

# PREAV

SELECT PREAV IF (0 ("fòrça")) (1C AJ) ;
REMOVE AV IF (0 PREAV) (0 AV) (1C AJ OR AV) ;
REMOVE PREAV IF (0 PREAV) (0 AV) (1C V) ;
REMOVE PREAV IF (0 PREAV) (0 AV) (NOT 1 AJ OR AV) ;

# ADJ POS

REMOVE AJ IF (NOT -1 Ser) (0 AJ + Pos) ;

# MOTS

SELECT Conjsub IF (-1 V) (0 Que) ;
  ## vôli que
SELECT Conjsub IF (-2 V) (1 AV) (0 Que) ;
  ## vôli pas que
SELECT Conjadv IF (-2 ("mai") OR ("pus") OR ("mens")) (-1 AJ OR VBpp OR AV) (0 Que) ;
  ## plus rapide que moi
SELECT Rel IF (-1 CM) (0 Que) ;
SELECT Rel IF (-1 DET) (0 Que) ;
SELECT Rel IF (-1C N) (0 Que) ;
SELECT Rel IF (-2C N) (-1 AJ) (0 Que) ;
SELECT Rel IF (-3 N) (-2 AV OR PREAV) (-1 AJ) (0 Que) ;

REMOVE PRON IF (0 Se) (NOT -1 Pro OR V + P3) ;
  ## se canti

SELECT N IF (0 ("<Estat>")) ;
SELECT N IF (-1 DET + Masc) (0 ("estat")) ;
SELECT VBpp IF (-1 Ser) (0 ("estat")) ;
SELECT VBpp IF (-2 Ser) (-1 AV) (0 ("estat")) ;
SELECT N IF (-1 ("vila") OR ("ciutat")) (0 ("estat")) ;	# vila estat

REMOVE AJ IF (0 Las) (1 N + FemPl) ;
  ## las pomas

SELECT Conjco IF (0 Mas) (-1 CM) ;
